apply plugin: 'com.android.application'
// Google Services Gradle plugin
apply plugin: 'com.google.gms.google-services'
// Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.tohsoft.app"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        release {
            try {
                storeFile file("TOHAndroidKS")
                storePassword KEYSTORE_PASSWORD
                keyAlias "tohandroidks"
                keyPassword KEY_PASSWORD
            } catch (ignored) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        encoding "UTF-8"
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    lintOptions {
        ignore 'ExtraTranslation', 'MissingTranslation'
        abortOnError false
    }

    sourceSets {
        pro.setRoot('src/pro')
        normal.setRoot('src/main')
        adDebug.setRoot('src/main')
    }

    flavorDimensions "capabilities"

    productFlavors {
        normal {
            dimension "capabilities"
            buildConfigField "boolean", "FULL_VERSION", "Boolean.parseBoolean(\"false\")"
            buildConfigField "boolean", "SHOW_AD", "Boolean.parseBoolean(\"true\")"
            buildConfigField "boolean", "TEST_AD", "Boolean.parseBoolean(\"false\")"
        }

        pro {
            dimension "capabilities"
            buildConfigField "boolean", "FULL_VERSION", "Boolean.parseBoolean(\"true\")"
            buildConfigField "boolean", "SHOW_AD", "Boolean.parseBoolean(\"false\")"
            buildConfigField "boolean", "TEST_AD", "Boolean.parseBoolean(\"false\")"
            applicationIdSuffix ".pro"
        }

        adDebug {
            dimension "capabilities"
            buildConfigField "boolean", "FULL_VERSION", "Boolean.parseBoolean(\"false\")"
            buildConfigField "boolean", "SHOW_AD", "Boolean.parseBoolean(\"true\")"
            buildConfigField "boolean", "TEST_AD", "Boolean.parseBoolean(\"true\")"
            versionName "0.001"
        }
    }

    // Tự động đổi tên apk và mapping file
    android.applicationVariants.all { variant ->
        def namePrefix = "Project_Template"
        if (getName().startsWith("pro")) {
            namePrefix = "Project_Template_Pro"
        }

        def apkName = namePrefix + "_v${variant.versionName}"
        variant.outputs.all {
            outputFileName =  apkName + ".apk"
        }

        if (!getName().contains("adDebug")) {
            variant.outputs.each { output ->
                if (variant.getBuildType().isMinifyEnabled()) {
                    variant.assemble.doLast {
                        copy {
                            from variant.mappingFile
                            into output.outputFile.parent
                            rename { String fileName ->
                                apkName + "_mapping.txt"
                            }
                        }
                    }
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "com.google.android.material:material:1.3.0-alpha01"
    implementation 'androidx.multidex:multidex:2.0.1'
    // Firebase
    implementation 'com.google.firebase:firebase-core:17.4.3'
    implementation 'com.google.firebase:firebase-config:19.1.4'
    implementation 'com.google.firebase:firebase-analytics:17.4.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.1'
    // Utility Library
    implementation 'com.github.phongnx:Utility:1.2.0'
    implementation 'com.blankj:utilcode:1.29.0'
    // Setup Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.8.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.1'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.7.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'
    // RxAndroid
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    // Glide image loader
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    // Material dialog
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    // Bind view Library
    implementation 'com.jakewharton:butterknife:10.2.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.1'
    // Event Bus
    implementation 'org.greenrobot:eventbus:3.2.0'
    // GSon
    implementation 'com.google.code.gson:gson:2.8.6'
    // Modules
    implementation project(':toh_rate_lib')
    implementation project(':TOH-Ads-SDK')
}
